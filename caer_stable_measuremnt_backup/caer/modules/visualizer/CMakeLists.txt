IF (NOT ENABLE_VISUALIZER)
	SET(ENABLE_VISUALIZER 0 CACHE BOOL "Enable the visualizer module (shows events, frames and IMU renderings)")
ENDIF()

IF (ENABLE_VISUALIZER)
	# Check module dependencies first.
	IF (NOT ENABLE_STATISTICS)
		MESSAGE(SEND_ERROR "Statistics module required by Visualizer module. Please enable it!")
		RETURN()
	ENDIF()

	SET(CAER_COMPILE_DEFINITIONS ${CAER_COMPILE_DEFINITIONS} -DENABLE_VISUALIZER=1 PARENT_SCOPE)

	# Visualizer support via Allegro library
	PKG_CHECK_MODULES(ALLEGRO5 allegro-5>=5.0.11)
	PKG_CHECK_MODULES(ALLEGRO5_MAIN allegro_main-5>=5.0.11)
	PKG_CHECK_MODULES(ALLEGRO5_PRIMITIVES allegro_primitives-5)
	PKG_CHECK_MODULES(ALLEGRO5_FONT allegro_font-5)
	PKG_CHECK_MODULES(ALLEGRO5_TTF allegro_ttf-5)

	IF (NOT ALLEGRO5_FOUND)
		# Check alternative pkg-config naming
		PKG_CHECK_MODULES(ALLEGRO5 allegro-5.0>=5.0.11)
		PKG_CHECK_MODULES(ALLEGRO5_MAIN allegro_main-5.0>=5.0.11)
		PKG_CHECK_MODULES(ALLEGRO5_PRIMITIVES allegro_primitives-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_FONT allegro_font-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_TTF allegro_ttf-5.0)
	ENDIF()

	IF (NOT ALLEGRO5_FOUND OR NOT ALLEGRO5_MAIN_FOUND)
		MESSAGE(SEND_ERROR "Allegro-5 main library not found!")
		RETURN()
	ENDIF()

	IF (NOT ALLEGRO5_PRIMITIVES_FOUND OR NOT ALLEGRO5_FONT_FOUND OR NOT ALLEGRO5_TTF_FOUND)
		MESSAGE(SEND_ERROR "Allegro-5 primitives, font and ttf addons not found!")
		RETURN()
	ENDIF()

	SET(VISUALIZER_LIBS ${ALLEGRO5_LIBRARIES} ${ALLEGRO5_MAIN_LIBRARIES})
	SET(VISUALIZER_INCDIRS ${ALLEGRO5_INCLUDE_DIRS} ${ALLEGRO5_MAIN_INCLUDE_DIRS})
	SET(VISUALIZER_LIBDIRS ${ALLEGRO5_LIBRARY_DIRS} ${ALLEGRO5_MAIN_LIBRARY_DIRS})

	SET(VISUALIZER_LIBS ${VISUALIZER_LIBS} ${ALLEGRO5_PRIMITIVES_LIBRARIES} ${ALLEGRO5_FONT_LIBRARIES} ${ALLEGRO5_TTF_LIBRARIES})
	SET(VISUALIZER_INCDIRS ${VISUALIZER_INCDIRS} ${ALLEGRO5_PRIMITIVES_INCLUDE_DIRS} ${ALLEGRO5_FONT_INCLUDE_DIRS} ${ALLEGRO5_TTF_INCLUDE_DIRS})
	SET(VISUALIZER_LIBDIRS ${VISUALIZER_LIBDIRS} ${ALLEGRO5_PRIMITIVES_LIBRARY_DIRS} ${ALLEGRO5_FONT_LIBRARY_DIRS} ${ALLEGRO5_TTF_LIBRARY_DIRS})

	SET(VISUALIZER_SRC_FILES modules/visualizer/visualizer.c)

	# Copy resources to build directory.
	FILE(COPY ${CMAKE_SOURCE_DIR}/ext/resources/LiberationSans-Regular.ttf DESTINATION ${CM_BUILD_DIR}/ext/resources)
	FILE(COPY ${CMAKE_SOURCE_DIR}/ext/resources/LiberationSans-Bold.ttf DESTINATION ${CM_BUILD_DIR}/ext/resources)

	# Install resources to a common system location.
	INSTALL(FILES ${CM_BUILD_DIR}/ext/resources/LiberationSans-Regular.ttf ${CM_BUILD_DIR}/ext/resources/LiberationSans-Bold.ttf DESTINATION ${CM_SHARE_DIR})

	# Propagate to parent scope.
	SET(CAER_INCDIRS ${CAER_INCDIRS} ${VISUALIZER_INCDIRS} PARENT_SCOPE)
	SET(CAER_LIBDIRS ${CAER_LIBDIRS} ${VISUALIZER_LIBDIRS} PARENT_SCOPE)
	SET(CAER_C_LIBS ${CAER_C_LIBS} ${VISUALIZER_LIBS} PARENT_SCOPE)
	SET(CAER_C_SRC_FILES ${CAER_C_SRC_FILES} ${VISUALIZER_SRC_FILES} PARENT_SCOPE)
ENDIF()
